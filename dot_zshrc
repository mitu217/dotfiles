umask 022
limit coredumpsize 0
bindkey -d

# Return if zsh is called from Vim
if [[ -n $VIMRUNTIME ]]; then
  return 0
fi

if [[ -f ~/.zplug/init.zsh ]]; then
  export ZPLUG_LOADFILE=~/.zsh/zplug.zsh
  source ~/.zplug/init.zsh

  if ! zplug check --verbose; then
    printf "Install? [y/N]: "
    if read -q; then
      echo; zplug install
    fi
    echo
  fi
  zplug load
fi

# ghq + fzf + tmux integration
# Select repository with fzf and create/attach tmux session
function tm() {
  local repo
  repo=$(ghq list | fzf --preview "bat --color=always --style=plain $(ghq root)/{}/README.md 2>/dev/null || ls -la $(ghq root)/{}") || return

  local session_name=$(basename "$repo" | tr '.' '-')
  local repo_path="$(ghq root)/$repo"

  if [[ -z "$TMUX" ]]; then
    # Outside tmux: create or attach
    tmux new-session -A -s "$session_name" -c "$repo_path"
  else
    # Inside tmux: switch or create session
    if ! tmux has-session -t "$session_name" 2>/dev/null; then
      tmux new-session -d -s "$session_name" -c "$repo_path"
    fi
    tmux switch-client -t "$session_name"
  fi
}

if [[ -f ~/.zshrc.local ]]; then
  source ~/.zshrc.local
fi
